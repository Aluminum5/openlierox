###
# Vermes
# Scons build file
# See INSTALL for instructions
###
import os
from glob import glob

###
# Create an environment
###
env = Environment()
libs = []

###
# Force toolset under Windows
###
if env['PLATFORM'] == 'win32':
	env = Environment(tools = ['mingw'])
	print "Forcing MinGW - MSVC++ not supported"


env.Append(CPPPATH = ['.', '../../include', '/usr/include/libxml2'])
env.ParseConfig("sdl-config --cflags --libs")

###
# The default program to build is vermes
###
env.Default('vermes' + env['PROGSUFFIX'])

###
# Libraries to be built from source
###
libs += ['omfgconsole', 'omfghttp', 'omfgutil', 'omfgui', 'omfgscript',
			'loadpng', 'glua', 'openlierox']

###
# Libraries to be picked up from the system
###
libs += ['SDL', 'SDL_image', 'boost_filesystem', 'boost_signals', 'png', 'z', 'alut',
			'openal', 'vorbisfile', 'xml2', 'crypto']

env.Append(CCFLAGS = '-ggdb -Wall')
env.Append(CCFLAGS = '-DDEBUG')

###
# Build the source libraries
###
env.StaticLibrary('omfgconsole', glob('console/*.cpp'))
env.StaticLibrary('omfghttp', glob('http/*.cpp'))
env.StaticLibrary('omfgutil', glob('util/*.cpp'))
env.StaticLibrary('omfgscript', glob('omfgscript/*.cpp'))
env.StaticLibrary('loadpng', glob('loadpng/*.c'))
env.StaticLibrary('omfgui', glob('gui/detail/*.cpp')
								+ glob('gui/lua/*.cpp'))
env.StaticLibrary('glua', glob('lua51/*.c') 
							+ glob('lua51/luaapi/*.cpp'))


#breakpad
#hawknl
#sdlimage

olxfiles = [
	"SystemFunctions.cpp",
	"ConfigHandler.cpp",
	"IniReader.cpp",
	"CScriptableVars.cpp",
	"Timer.cpp",
	"EventQueue.cpp",
	"MainGlobals.cpp",
	"TeeStdoutHandler.cpp",
	"Mutex.cpp",
	"Networking.cpp",
	"TaskManager.cpp",
	"ThreadPool.cpp",
	"Unicode.cpp",
	"Version.cpp",
	"SMTP.cpp",
	"CrashHandler.cpp",
	"StringBuf.cpp",
	"Debug.cpp",
	"StringUtils.cpp",
	"FindFile.cpp",
	"InputEvents.cpp",
	"CInput.cpp",
	"GfxPrimitives.cpp",
	"Geometry.cpp",
	"MathLib.cpp",
	"Options.cpp",
	"FeatureList.cpp" ]

def findFullOlxFn(fn):
	olxsrcdir = ".."
	for root, _, files in os.walk(olxsrcdir):
		if fn in files:
			return root + "/" + fn
	print "ERROR:", fn, "not found in OLX!"
	return None


env.StaticLibrary('openlierox',
	[findFullOlxFn(x) for x in olxfiles] +
	[ "../breakpad/ExtractInfo.cpp" ]
	)

env.Append(CCFLAGS = "-DNBREAKPAD")


###
# Build the main program
###
env.Program('vermes', glob('*.cpp') + glob('loaders/*.cpp')
						+ glob('lua/*.cpp') + glob('blitters/*.cpp')
						+ ['util/text.o']
					, LIBS = libs
					, LIBPATH = ['.', '/usr/local/lib', '/usr/lib'])

